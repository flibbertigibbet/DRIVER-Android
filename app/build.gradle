buildscript {
    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.github.WorldBank-Transport.jsonschema2pojo:jsonschema2pojo-gradle-plugin:jsonschema2pojo-0.4.18-target-7'
        classpath 'com.google.code.gson:gson:2.4'
        classpath 'org.joda:joda-convert:1.8.1'
        classpath 'javax.annotation:javax.annotation-api:1.2'
        classpath 'com.google.code.findbugs:jsr305:3.0.1'
        classpath 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
        classpath 'joda-time:joda-time:2.9.1'
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'ant', module: 'ant'
    all*.exclude group: 'awtui', module: 'awtui'
    all*.exclude group: 'swingui', module: 'swingui'
}

apply plugin: 'jsonschema2pojo'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    // Must specify individually all duplicated files from libraries to exclude or pickFirst
    packagingOptions {
        exclude 'NOTICE'
        exclude 'NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
    }
    defaultConfig {
        applicationId "org.worldbank.transport.driver"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        // can enable this to allow >50K methods
        //multiDexEnabled true

        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-test.pro'
        }

        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-test.pro'
        }
    }
    dexOptions {
        incremental true
        javaMaxHeapSize "4096M"
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile 'com.google.code.gson:gson:2.4'
    compile('com.github.WorldBank-Transport:jsonschema2pojo:jsonschema2pojo-0.4.18-target-7') {
        // only want core
        exclude module: 'jsonschema2pojo-ant'
        exclude module: 'jsonschema2pojo-cli'
        exclude module: 'jsonschema2pojo-maven-plugin'
        exclude module: 'jsonschema2pojo-gradle-plugin'
        exclude module: 'jsonschema2pojo-integration-tests'

        exclude group: 'javax.validation', module: 'validation-api'
        exclude group: 'junit', module: 'junit'
        exclude group: 'android', module: 'net'
    }
    // used by jsonschema2pojo
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'commons-lang:commons-lang:2.6'
    compile 'joda-time:joda-time:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'org.joda:joda-convert:1.8.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'

    // form builder
    compile 'com.github.azavea:AndroidValidatedForms:1.0.6'

    // test dependencies
    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.2'

    // mock web server and friends
    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-testing-support:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-ws:2.7.0'
    androidTestCompile 'org.bouncycastle:bcprov-jdk15on:1.53'
    androidTestCompile 'com.squareup.okio:okio:1.6.0'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'com.squareup.okhttp:okhttp-android-support:2.7.0'
    androidTestCompile 'com.squareup.okhttp:okhttp-urlconnection:2.7.0'
    androidTestCompile 'org.codehaus.mojo:animal-sniffer-annotations:1.14'

    // Android libraries
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    //compile 'com.android.support:multidex:1.0.1'
}

jsonSchema2Pojo {
    // Whether to generate builder-style methods of the form withXxx(value) (that return this),
    // alongside the standard, void-return setters.
    generateBuilders = false

    usePrimitives = false

    // Location of the JSON Schema file(s). This may refer to a single file or a directory of files.
    //source = files("${sourceSets.main.output.resourcesDir}/json")
    source = files("src/main/assets/json/schemas")

    // Target directory for generated Java source files. The plugin will add this directory to the
    // java source set so the compiler will find and compile the newly generated source files.
    targetDirectory = file("${project.buildDir}/generated/source/js2p")

    // Package name used for generated Java classes (for types where a fully qualified name has not
    // been supplied in the schema using the 'javaType' property).
    targetPackage = 'org.worldbank.transport.driver.models'

    // The characters that should be considered as word delimiters when creating Java Bean property
    // names from JSON property names. If blank or not set, JSON properties will be considered to
    // contain a single word when creating Java Bean property names.
    propertyWordDelimiters = [' ','_'] as char[]

    // Whether to use the java type long (or Long) instead of int (or Integer) when representing the
    // JSON Schema type 'integer'.
    useLongIntegers = false

    // Whether to use the java type double (or Double) instead of float (or Float) when representing
    // the JSON Schema type 'number'.
    useDoubleNumbers = true

    // Whether to include hashCode and equals methods in generated Java types.
    includeHashcodeAndEquals = true

    // Whether to include a toString method in generated Java types.
    includeToString = true

    includeAccessors = false

    includeAdditionalProperties = false

    annotationStyle = 'gson'

    //customAnnotator = 'org.jsonschema2pojo.NoopAnnotator'
    customAnnotator = 'org.jsonschema2pojo.JsonEditorAnnotator'

    includeJsr303Annotations = true

    sourceType = 'jsonschema'

    removeOldOutput = true

    outputEncoding = 'UTF-8'

    useJodaDates = false

    useCommonsLang3 = false

    // Whether to initialize Set and List fields as empty collections, or leave them as null.
    initializeCollections = true

    // **EXPERIMENTAL** Whether to make the generated types Parcelable for Android
    parcelable = false
}
